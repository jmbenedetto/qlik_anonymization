//#########################################################################################//
/* -------------------------------------------------------------------

    Name : Anon_FakeItSelfDimension
    
----------------------------------------------------------------------
 	Original Rule :	Replace Generic Master Data , from Inline possibles
 	Params :
    	1 - Delete Original Field (Y/N)
		2 - Table Name
        3 - Field Name
 
-------------------------------------------------------------------*/


SUB Anon_FakeItSelfDimension (P_DELETE_ORIGINAL_FIELD , P_TABLENAME , P_FIELDNAME)

	
TRACE ####################################################;
TRACE ## Starting Function : Anon_FakeFieldDimension  ##;
TRACE ## Anonymizing Field : $(P_FIELDNAME) #;
TRACE ####################################################;

    
    
    //---------------------------------------//
    //-- Fake Name Table dynamic auto rows --//
    
    TempDistinct:
    Load Distinct [$(P_FIELDNAME)] Resident [$(P_TABLENAME)] where not IsNull([$(P_FIELDNAME)]);
    
    Let vTotalLines = NoOfRows('TempDistinct');
    
    TRACE # Total Distinct Dimension Values : $(vTotalLines) #;
    
    //-------------------------------------------------------------------//
    //-- Including External QVS with ALL current and future Inlines

	FieldTable:
    LOAD
    	'$(P_FIELDNAME)' as Field
    AutoGenerate 1;
        
    Join (FieldTable)

    Load 
    	RowNo() as Multiply
    Autogenerate $(vTotalLines);   //--> Multiply by exatcly amount of rows
    
    //-------------------------------------------------------------------//
    //-- Mapping table with True Name + Fake Name (ensure that the same true name will always be given the same fake name)
    
        NoConcatenate

        MappingTMP:
        Load RowNo() 					 as Line,
        	 [$(P_FIELDNAME)]			 as True
        Resident TempDistinct;
        
        Left Join (MappingTMP)
        
        LOAD RowNo() 					 			as Line,
             Field & ' - ' & Multiply  				as Fake
        Resident FieldTable;  
        
        Drop table TempDistinct;
        Drop table FieldTable;
        
    //-------------------------------------------------------------------//
    
    // -- Final Fake Dimension Table with the exactly amount of lines , based on the selected INLINE map -- //
    
        [Map_$(P_FIELDNAME)]:
        Mapping
        LOAD
            True,
            Fake
        Resident MappingTMP;

        Drop table MappingTMP;
	
    
    //-- END Fake Table --//
    //---------------------------------------//
        
	NoConcatenate
    
	[Anon_$(P_TABLENAME)]:
    LOAD
    	*,
        
        ApplyMap('Map_$(P_FIELDNAME)',[$(P_FIELDNAME)],Null())		as [Anon.$(P_FIELDNAME)]
        
        
    Resident [$(P_TABLENAME)];
    
    Drop table [$(P_TABLENAME)];
    
    Rename table [Anon_$(P_TABLENAME)] to [$(P_TABLENAME)];
    
    IF MixMatch(P_DELETE_ORIGINAL_FIELD,'Y') THEN
    	Drop field [$(P_FIELDNAME)] from [$(P_TABLENAME)];
    END IF; 
    
// -- SUB END -- //    

TRACE ##################################################;
TRACE # Field anonymized successfully : $(P_FIELDNAME) #;
TRACE ##################################################;

END SUB

//#########################################################################################//